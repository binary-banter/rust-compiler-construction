use crate::passes::parse::{
    BinaryOp, DefParsed, ExprParsed, InstrParsed, Lit, Meta, Param, PrgParsed, Spanned, Type, TypeDef, UnaryOp,
};
use crate::passes::select::{VarArg, Reg};
use functor_derive::Functor;

grammar;

match {
    // Keywords
    "fn",
    "let",
    "if",
    "else",
    "loop",
    "while",
    "break",
    "continue",
    "return",
    "mut",
    "struct",
    "enum",
    "switch",
    "asm",

    // Asm instructions
    "addq",
    "subq",
    "mulq",
    "divq",
    "negq",
    "movq",
    "pushq",
    "popq",
    "retq",
    "andq",
    "orq",
    "xorq",
    "notq",
    "cmpq",
    "syscall",

    // Asm registers
    "%RSP",
    "%RBP",
    "%RAX",
    "%RBX",
    "%RCX",
    "%RDX",
    "%RSI",
    "%RDI",
    "%R8",
    "%R9",
    "%R10",
    "%R11",
    "%R12",
    "%R13",
    "%R14",
    "%R15",

    // Structural tokens
    "(",
    ")",
    "{",
    "}",
    "[",
    "]",
    "->",
    "=",
    ";",
    ",",
    ":",
    ".",
    "::",
    "=>",
    "$",

    // Identifier
    r"[_a-zA-Z][_a-zA-Z0-9]*" => identifier,

    // Integer operators
    "+",
    "-",
    "*",
    "/",
    "%",

    // Types
    "I64",
    "U64",
    "Bool",
    "Unit",
    "Never",

    // Literals
    "true",
    "false",
    "unit",
    r"[0-9]([a-zA-Z0-9_]*[a-zA-Z0-9])?|b'(\\)?[^'[[:cntrl:]]]'([iu]64)?" => integer,

    // Logical operators
    "^",
    "&&",
    "||",
    "!",

    // Compartive operators
    "==",
    "!=",
    ">",
    ">=",
    "<",
    "<=",

    // Whitespaces
    r"\s+" => {},

    // Line comments
    r"//[^\n]*\n" => {},

    // Block comments
    r"/\*([^*]|(\*[^/]))*\*/" => {},
}

pub Program: PrgParsed<'input> = {
    <defs: Def*> => PrgParsed {
        defs,
        entry: "main",
    }
}

Def: DefParsed<'input> = {
    "struct" <sym:Ident> "{" <fields:Comma<(<Ident> ":" <Type>)>> "}" => DefParsed::TypeDef {
        sym,
        def: TypeDef::Struct { fields },
    },
    "enum" <sym:Ident> "{" <variants:Comma<(<Ident> ":" <Type>)>> "}" => DefParsed::TypeDef {
        sym,
        def: TypeDef::Enum { variants },
    },
    "fn" <sym:Ident> "(" <params:Comma<Param>> ")" <typ:("->" <Type>)?> "{" <bdy: Spanned<Expr?>> "}" => DefParsed::Fn {
        sym,
        params,
        typ: typ.unwrap_or(Type::Unit),
        bdy: bdy.fmap(|bdy| bdy.unwrap_or(ExprParsed::Lit { val: Lit::Unit })),
    },
}

Param: Param<Spanned<&'input str>> = {
    <mutable:"mut"?> <sym:Ident> ":" <typ:Type> => Param {
        mutable: mutable.is_some(),
        sym,
        typ,
    }
}

Type: Type<Spanned<&'input str>> = {
    "I64" => Type::I64,
    "U64" => Type::U64,
    "Bool" => Type::Bool,
    "Unit" => Type::Unit,
    "Never" => Type::Never,
    <sym:Ident> => Type::Var { sym },
}

// We have the following precedence:
// ExprStmt
// ExprInStmt
// ExprLogicalOr
// ExprLogicalAnd
// ExprComparative
// ExprXor
// ExprAdditive
// ExprMultiplicative
// ExprUnary
// ExprAccess
// ExprCall
// ExprAtom
// Num/Bool/Ident
Expr = ExprStmt;

ExprStmt: ExprParsed<'input> = {
    "let" <mutable:"mut"?> <sym:Ident> <typ:(":" <Type>)?> "=" <bnd:Spanned<ExprLogicalOr<Struct>>> ";" <bdy:Spanned<ExprStmt?>> => ExprParsed::Let {
        sym,
        mutable: mutable.is_some(),
        typ,
        bnd: Box::new(bnd),
        bdy: Box::new(bdy.fmap(|bdy| bdy.unwrap_or(ExprParsed::Lit { val: Lit::Unit }))),
    },
    <stmt:Spanned<ExprInStmt>> ";" <cnt:Spanned<ExprStmt?>> => ExprParsed::Seq {
        stmt: Box::new(stmt),
        cnt: Box::new(cnt.fmap(|cnt| cnt.unwrap_or(ExprParsed::Lit { val: Lit::Unit }))),
    },
    ExprInStmt,
}

ExprInStmt: ExprParsed<'input> = {
    <sym:Ident> "=" <bnd:Spanned<ExprLogicalOr<Struct>>> => ExprParsed::Assign {
        sym,
        bnd: Box::new(bnd),
    },
    ExprIf,
    "loop" "{" <bdy:Spanned<Expr>> "}" => ExprParsed::Loop {
        bdy: Box::new(bdy),
    },
    // todo: the spans in this desugaring do not make a lot sense.
    <l:@L> "while" <r:@R> <cnd:Spanned<ExprLogicalOr<Never>>> "{" <bdy:Spanned<Expr>> "}" => ExprParsed::Loop {
        bdy: Box::new(Meta {
            meta: (l, r - l),
            inner: ExprParsed::If {
                cnd: Box::new(cnd),
                thn: Box::new(bdy),
                els: Box::new(Meta {
                    meta: (l, r - l),
                    inner: ExprParsed::Seq {
                        stmt: Box::new(Meta {
                            meta: (l, r - l),
                            inner: ExprParsed::Break { bdy: Box::new(Meta {
                                meta: (l, r - l),
                                inner: ExprParsed::Lit { val: Lit::Unit },
                            })},
                        }),
                        cnt: Box::new(Meta {
                            meta: (l, r - l),
                            inner: ExprParsed::Lit { val: Lit::Unit },
                        }),
                    },
                }),
            },
        }),
    },
    "switch" <enm:Spanned<ExprLogicalOr<Never>>> "{" <arms:Comma<(<Ident> "(" <Ident> ")" "=>" <Spanned<Expr>> )>> "}"  => ExprParsed::Switch {
        enm: Box::new(enm),
        arms: arms.into_iter().map(|(s1, s2, e)| (s1, s2, Box::new(e))).collect(),
     },
    "break" <bdy:Spanned<ExprLogicalOr<Struct>?>> => ExprParsed::Break {
        bdy: Box::new(bdy.fmap(|bdy| bdy.unwrap_or(ExprParsed::Lit { val: Lit::Unit }))),
    },
    "return" <bdy:Spanned<ExprLogicalOr<Struct>?>> => ExprParsed::Return {
        bdy: Box::new(bdy.fmap(|bdy| bdy.unwrap_or(ExprParsed::Lit { val: Lit::Unit }))),
    },
    "continue" => ExprParsed::Continue,
    "asm" "{" <instrs:AsmInstr*> "}" => ExprParsed::Asm { instrs },
    ExprLogicalOr<Struct>,
}

ExprIf: ExprParsed<'input> = {
    "if" <cnd:Spanned<ExprLogicalOr<Never>>> "{" <thn:Spanned<Expr>> "}" <l:@L> <els:("else" <Spanned<ExprIfElse>>)?> <r:@R> => ExprParsed::If {
        cnd: Box::new(cnd),
        thn: Box::new(thn),
        els: Box::new(els.unwrap_or(Meta { meta: (l, r - l), inner: ExprParsed::Lit { val: Lit::Unit }})),
    },
}

ExprIfElse: ExprParsed<'input> = {
    "{" <Expr> "}",
    ExprIf,
}

BinaryOps<Op,Next>: ExprParsed<'input> = {
    <e1:Spanned<BinaryOps<Op,Next>>> <op:Op> <e2:Spanned<Next>> => ExprParsed::BinaryOp {
        op,
        exprs: [Box::new(e1), Box::new(e2)],
    },
    Next,
}

ExprLogicalOr<T> = BinaryOps<LogicalOrOp, ExprLogicalAnd<T>>;
ExprLogicalAnd<T> = BinaryOps<LogicalAndOp, ExprComparative<T>>;
ExprComparative<T> = BinaryOps<ComparativeOp, ExprXor<T>>;
ExprXor<T> = BinaryOps<XorOp, ExprAdditive<T>>;
ExprAdditive<T> = BinaryOps<AdditiveOp, ExprMultiplicative<T>>;
ExprMultiplicative<T> = BinaryOps<MultiplicativeOp, ExprUnary<T>>;

LogicalOrOp: BinaryOp = "||" => BinaryOp::LOr;
LogicalAndOp: BinaryOp = "&&" => BinaryOp::LAnd;
ComparativeOp: BinaryOp = {
    "==" => BinaryOp::EQ,
    "!=" => BinaryOp::NE,
    ">" => BinaryOp::GT,
    ">=" => BinaryOp::GE,
    "<" => BinaryOp::LT,
    "<=" => BinaryOp::LE,
}
XorOp: BinaryOp = "^" => BinaryOp::Xor;
AdditiveOp: BinaryOp = {
    "+" => BinaryOp::Add,
    "-" => BinaryOp::Sub,
}
MultiplicativeOp: BinaryOp = {
   "*" => BinaryOp::Mul,
   "/" => BinaryOp::Div,
   "%" => BinaryOp::Mod,
}
UnaryOp: UnaryOp = {
    "-" => UnaryOp::Neg,
    "!" => UnaryOp::Not,
}

ExprUnary<T>: ExprParsed<'input> = {
    <op:UnaryOp> <e:Spanned<ExprUnary<T>>> => ExprParsed::UnaryOp {
        op,
        expr: Box::new(e),
    },
    ExprAccess<T>,
}

ExprAccess<T>: ExprParsed<'input> = {
    <strct:Spanned<ExprAccess<T>>> "." <field:Ident> => ExprParsed::AccessField {
        strct: Box::new(strct),
        field,
    },
    ExprCall<T>,
}

ExprCall<T>: ExprParsed<'input> = {
    <fun:Spanned<ExprAtom<T>>> "(" <args:Comma<Spanned<Expr>>> ")" => ExprParsed::Apply {
        fun: Box::new(fun),
        args,
    },
    ExprAtom<T>,
}

ExprAtom<T>: ExprParsed<'input> = {
    <val:integer> => ExprParsed::Lit {
        val: Lit::Int {
            val,
        },
    },
    <val:Bool> => ExprParsed::Lit { val: Lit::Bool { val } },
    "unit" => ExprParsed::Lit { val: Lit::Unit },
    <sym:Ident> => ExprParsed::Var { sym },
    <enum_sym:Ident> "::" <variant_sym:Ident> "(" <bdy:Spanned<Expr>> ")" => ExprParsed::Variant {
        enum_sym,
        variant_sym,
        bdy: Box::new(bdy),
    },
    "(" <Expr> ")",
    <T>,
}

Struct: ExprParsed<'input> = {
    <sym:Ident> "{" <fields:Comma<StructArg>> "}" => ExprParsed::Struct {
        sym,
        fields,
    },
}

StructArg: (Spanned<&'input str>, Spanned<ExprParsed<'input>>) = {
    <Ident> ":" <Spanned<Expr>>,
    <l:@L> <sym:Ident> <r:@R> => (sym.clone(), Meta { meta: (l, r - l), inner: ExprParsed::Var { sym } })
}

Never: ExprParsed<'input> = {};

Ident: Spanned<&'input str> = Spanned<identifier>;

Bool: bool = {
    "true" => true,
    "false" => false,
}

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

Spanned<T>: Spanned<T> = <l:@L> <inner:T> <r:@R> => Meta { meta: (l, r - l), inner };

AsmInstr: InstrParsed<'input> = {
    "addq" <src:AsmArg> <dst:AsmArg> => InstrParsed::Addq { src, dst },
    "subq" <src:AsmArg> <dst:AsmArg> => InstrParsed::Subq { src, dst },
    "mulq" <src:AsmArg> => InstrParsed::Mulq { src },
    "divq" <divisor:AsmArg> => InstrParsed::Divq { divisor },
    "negq" <dst:AsmArg> => InstrParsed::Negq { dst },
    "movq" <src:AsmArg> <dst:AsmArg> => InstrParsed::Movq { src, dst },
    "pushq" <src:AsmArg> => InstrParsed::Pushq { src },
    "popq" <dst:AsmArg> => InstrParsed::Popq { dst },
    "retq" => InstrParsed::Retq,
    "andq" <src:AsmArg> <dst:AsmArg> => InstrParsed::Andq { src, dst },
    "orq" <src:AsmArg> <dst:AsmArg> => InstrParsed::Orq { src, dst },
    "xorq" <src:AsmArg> <dst:AsmArg> => InstrParsed::Xorq { src, dst },
    "notq" <dst:AsmArg> => InstrParsed::Notq { dst },
    "cmpq" <src:AsmArg> <dst:AsmArg> => InstrParsed::Cmpq { src, dst },
    "syscall" <arity:integer> => InstrParsed::Syscall { arity: arity.parse().expect("internal compiler error :(") },
}

AsmArg: VarArg<Spanned<&'input str>> = {
    <reg:AsmReg> => VarArg::Reg { reg },
    "{" <sym:Ident> "}" => VarArg::XVar { sym },
    "$" <val:integer> => VarArg::Imm {
        val: val.parse().expect("Internal compiler error (oh no!): We were too lazy to make a proper error for this"),
    },
    "[" <reg:AsmReg> "+" <off:integer> "]"  => VarArg::Deref {
        reg,
        off: off.parse().expect("Internal compiler error (oh no!): We were too lazy to make a proper error for this"),
    },
}

AsmReg: Reg = {
    "%RSP" => Reg::RSP,
    "%RBP" => Reg::RBP,
    "%RAX" => Reg::RAX,
    "%RBX" => Reg::RBX,
    "%RCX" => Reg::RCX,
    "%RDX" => Reg::RDX,
    "%RSI" => Reg::RSI,
    "%RDI" => Reg::RDI,
    "%R8" => Reg::R8 ,
    "%R9" => Reg::R9 ,
    "%R10" => Reg::R10,
    "%R11" => Reg::R11,
    "%R12" => Reg::R12,
    "%R13" => Reg::R13,
    "%R14" => Reg::R14,
    "%R15" => Reg::R15,
}
