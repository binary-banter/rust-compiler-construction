use std::str::FromStr;
use crate::passes::parse::{Def, Expr, Lit, Op, Param};
use crate::passes::parse::PrgParsed;
use crate::passes::parse::Type;

grammar;

match {
    // Keywords
    "fn",
    "let",
    "if",
    "else",
    "loop",
    "while",
    "break",
    "continue",
    "return",
    "mut",
    "struct",
    "enum",
    "switch",
    "never",

    // Structural tokens
    "(",
    ")",
    "{",
    "}",
    "->",
    "=",
    ";",
    ",",
    ":",
    ".",
    "::",
    "=>",

    // Identifier
    r"[_a-zA-Z][_a-zA-Z0-9]*",

    // Integer operators
    "+",
    "-",
    "*",
    "/",
    "%",

    // Cursed build-in functions
    "read",
    "print",

    // Types
    "Int",
    "Bool",
    "Unit",
    "Never",

    // Literals
    "true",
    "false",
    "unit",
    r"[0-9]+",

    // Logical operators
    "^",
    "&&",
    "||",
    "!",

    // Compartive operators
    "==",
    "!=",
    ">",
    ">=",
    "<",
    "<=",

    // Whitespaces
    r"\s+" => {},

    // Line comments
    r"//[^\n]*\n" => {},

    // Block comments
    r"/\*([^*]|(\*[^/]))*\*/" => {},
}

pub Program: PrgParsed<'input> = {
    <defs: Def*> => PrgParsed {
        defs,
        entry: "main"
    }
}

Def: Def<&'input str, Expr<&'input str>> = {
    "struct" <sym:Ident> "{" <fields:Comma<(<Ident> ":" <Type>)>> "}" => Def::Struct {
        sym,
        fields,
    },
    "enum" <sym:Ident> "{" <variants:Comma<(<Ident> ":" <Type>)>> "}" => Def::Enum {
        sym,
        variants,
    },
    "fn" <sym:Ident> "(" <params:Comma<Param>> ")" <typ:("->" <Type>)?> "{" <bdy: Expr> "}" => Def::Fn {
        sym,
        params,
        typ: typ.unwrap_or(Type::Unit),
        bdy
    },
}

Param: Param<&'input str> = {
    <mutable:"mut"?> <sym:Ident> ":" <typ:Type> => Param {
        mutable: mutable.is_some(),
        sym,
        typ,
    }
}

Type: Type<&'input str> = {
    "Int" => Type::Int,
    "Bool" => Type::Bool,
    "Unit" => Type::Unit,
    "Never" => Type::Never,
    <sym:Ident> => Type::Var { sym },
}

// We have the following precedence:
// ExprStmt
// ExprInStmt
// ExprLogicalOr
// ExprLogicalAnd
// ExprComparative
// ExprXor
// ExprAdditive
// ExprMultiplicative
// ExprUnary
// ExprAccess
// ExprCall
// ExprAtom
// Num/Bool/Ident
Expr = ExprStmt;

ExprStmt: Expr<&'input str> = {
    "let" <mutable:"mut"?> <sym:Ident> "=" <bnd:ExprLogicalOr<Struct>> ";" <bdy:ExprStmt?> => Expr::Let {
        sym,
        mutable: mutable.is_some(),
        bnd: Box::new(bnd),
        bdy: Box::new(bdy.unwrap_or(Expr::Lit { val: Lit::Unit })),
    },
    <stmt:ExprInStmt> ";" <cnt:ExprStmt?> => Expr::Seq {
        stmt: Box::new(stmt),
        cnt: Box::new(cnt.unwrap_or(Expr::Lit { val: Lit::Unit })),
    },
    ExprInStmt,
}

ExprInStmt: Expr<&'input str> = {
    <sym:Ident> "=" <bnd:ExprLogicalOr<Struct>> => Expr::Assign {
        sym,
        bnd: Box::new(bnd),
    },
    "if" <cnd:ExprLogicalOr<Never>> "{" <thn:Expr> "}" <els:("else" "{" <Expr> "}")?> => Expr::If {
        cnd: Box::new(cnd),
        thn: Box::new(thn),
        els: Box::new(els.unwrap_or(Expr::Lit { val: Lit::Unit })),
    },
    "loop" "{" <bdy:Expr> "}" => Expr::Loop {
        bdy: Box::new(bdy),
    },
    "while" <cnd:ExprLogicalOr<Never>> "{" <bdy:Expr> "}" => Expr::Loop {
        bdy: Box::new(Expr::If {
            cnd: Box::new(cnd),
            thn: Box::new(bdy),
            els: Box::new(Expr::Seq {
                stmt: Box::new(Expr::Break { bdy: Box::new(Expr::Lit { val: Lit::Unit }) }),
                cnt: Box::new(Expr::Lit { val: Lit::Unit }),
            }),
        }),
    },
    "switch" <enm:ExprLogicalOr<Never>> "{" <arms:Comma<(<Ident> "(" <Ident> ")" "=>" <Expr> )>> "}"  => Expr::Switch {
        enm: Box::new(enm),
        arms: arms.into_iter().map(|(s1, s2, e)| (s1, s2, Box::new(e))).collect(),
     },
    "break" <bdy:ExprLogicalOr<Struct>?> => Expr::Break {
        bdy: Box::new(bdy.unwrap_or(Expr::Lit { val: Lit::Unit })),
    },
    "return" <bdy:ExprLogicalOr<Struct>?> => Expr::Return {
        bdy: Box::new(bdy.unwrap_or(Expr::Lit { val: Lit::Unit })),
    },
    "continue" => Expr::Continue,
    ExprLogicalOr<Struct>,
}

BinaryOps<Op,Next>: Expr<&'input str> = {
    <e1:BinaryOps<Op,Next>> <op:Op> <e2:Next> => Expr::Prim {
        op,
        args: vec![e1, e2],
    },
    Next,
}

ExprLogicalOr<T> = BinaryOps<LogicalOrOp, ExprLogicalAnd<T>>;
ExprLogicalAnd<T> = BinaryOps<LogicalAndOp, ExprComparative<T>>;
ExprComparative<T> = BinaryOps<ComparativeOp, ExprXor<T>>;
ExprXor<T> = BinaryOps<XorOp, ExprAdditive<T>>;
ExprAdditive<T> = BinaryOps<AdditiveOp, ExprMultiplicative<T>>;
ExprMultiplicative<T> = BinaryOps<MultiplicativeOp, ExprUnary<T>>;

LogicalOrOp: Op = "||" => Op::LOr;
LogicalAndOp: Op = "&&" => Op::LAnd;
ComparativeOp: Op = {
    "==" => Op::EQ,
    "!=" => Op::NE,
    ">" => Op::GT,
    ">=" => Op::GE,
    "<" => Op::LT,
    "<=" => Op::LE,
}
XorOp: Op = "^" => Op::Xor;
AdditiveOp: Op = {
    "+" => Op::Plus,
    "-" => Op::Minus,
}
MultiplicativeOp: Op = {
   "*" => Op::Mul,
   "/" => Op::Div,
   "%" => Op::Mod,
}
UnaryOp: Op = {
    "-" => Op::Minus,
    "!" => Op::Not,
}

ExprUnary<T>: Expr<&'input str> = {
    <op:UnaryOp> <e:ExprUnary<T>> => Expr::Prim {
        op,
        args: vec![e],
    },
    ExprAccess<T>,
}

ExprAccess<T>: Expr<&'input str> = {
    <strct:ExprCall<T>> "." <field:Ident> => Expr::AccessField {
        strct: Box::new(strct),
        field,
    },
    ExprCall<T>,
}

ExprCall<T>: Expr<&'input str> = {
    "read" "(" ")" => Expr::Prim {
        op: Op::Read,
        args: vec![],
    },
    "print" "(" <e: Expr> ")" => Expr::Prim {
        op: Op::Print,
        args: vec![e],
    },
    <fun:ExprAtom<T>> "(" <args:Comma<Expr>> ")" => Expr::Apply {
        fun: Box::new(fun),
        args,
    },
    ExprAtom<T>,
}

ExprAtom<T>: Expr<&'input str> = {
    <val:Num> => Expr::Lit{ val: Lit::Int { val }},
    <val:Bool> => Expr::Lit { val: Lit::Bool { val }},
    "unit" => Expr::Lit { val: Lit::Unit },
    <sym:Ident> => Expr::Var { sym },
    <enum_sym:Ident> "::" <variant_sym:Ident> "(" <bdy:Expr> ")" => Expr::Variant {
        enum_sym,
        variant_sym,
        bdy: Box::new(bdy),
    },
    "(" <Expr> ")",
    <T>,
}

Struct: Expr<&'input str> = {
    <sym:Ident> "{" <fields:Comma<(<Ident> ":" <Expr>)>> "}" => Expr::Struct {
        sym,
        fields,
    },
}

Never: Expr<&'input str> = {
    "never" => panic!("The reserved keyword 'never' should never be parsed.")
}

Ident: &'input str = r"[_a-zA-Z][_a-zA-Z0-9]*";

Num: i64 = <s:r"[0-9]+"> => i64::from_str(s).unwrap();

Bool: bool = {
    "true" => true,
    "false" => false,
}

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}
